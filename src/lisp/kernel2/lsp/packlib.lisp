;;;;  -*- Mode: Lisp; Syntax: Common-Lisp; Package: SYSTEM -*-
;;;;
;;;;  Copyright (c) 1984, Taiichi Yuasa and Masami Hagiya.
;;;;  Copyright (c) 1990, Giuseppe Attardi.
;;;;  Copyright (c) 2001, Juan Jose Garcia Ripoll.
;;;;
;;;;    This program is free software; you can redistribute it and/or
;;;;    modify it under the terms of the GNU Library General Public
;;;;    License as published by the Free Software Foundation; either
;;;;    version 2 of the License, or (at your option) any later version.
;;;;
;;;;    See file '../Copyright' for full details.

;;;;                    package routines

(in-package "SYSTEM")

(defun find-all-symbols (string-or-symbol)
  "Args: (string-designator)
Returns a list of all symbols that have the specified print name.
STRING-DESIGNATOR may be a symbol, in which case the print name of the symbol
is used."
  (let ((symbol-name (string string-or-symbol)))
    (mapcan #'(lambda (p)
		(multiple-value-bind (s i)
		    (find-symbol symbol-name p)
		  (if (or (eq i :internal) (eq i :external))
		      (list s)
		      nil)))
	    (list-all-packages))))

(defun packages-iterator (packages options maybe-list)
  (let ((all-symbols nil))
    (when (or (atom packages) (not maybe-list))
      (setq packages (list packages)))
    (dolist (p packages)
      (let ((package (si::coerce-to-package p)))
	(multiple-value-bind (hash-ext hash-int packages-used)
	    (si::package-hash-tables package)
	  (when (member :external options)
	    (push (list package :external hash-ext) all-symbols))
	  (when (member :internal options)
	    (push (list package :internal hash-int) all-symbols))
	  (when (member :inherited options)
	    (dolist (p packages-used)
	      (push (list package :inherited (si::package-hash-tables p))
		    all-symbols))))))
    (unless all-symbols
      (return-from packages-iterator #'(lambda () (values nil nil nil nil))))
    (let* ((current (pop all-symbols))
	   (package (first current))
	   (type (second current))
	   (iterator (si::hash-table-iterator (third current))))
      (flet ((iterate ()
               (declare (core:lambda-name packages-iterator-iterate))
	       (tagbody
		AGAIN
		  (multiple-value-bind (found key value)
		      (funcall iterator)
		    (declare (ignore key))
		    (cond 
		      (found
		       (when (eq type :inherited)
			 (multiple-value-bind (s access)
			     (find-symbol (symbol-name value) package)
			   (unless (and (eq s value) (eq access type))
			     (go AGAIN))))
		       (return-from iterate (values t value type package)))
		      ((null all-symbols)
		       (return-from iterate (values nil nil nil nil)))
		      (t
		       (setq current (pop all-symbols))
		       (setq package (first current)
			     type (second current)
			     iterator (si::hash-table-iterator (third current))
			     ))))
		  (go AGAIN))))
	#'iterate))))

(defmacro with-package-iterator ((iterator package-list &rest conditions)
				 &rest body)
  (if conditions
      (let ((aux (set-difference conditions '(:external :internal :inherited))))
	(when aux
          (simple-program-error  "Clauses 誉狎铒犰祜麇洚狨┅箝眇戾痱镧蜥憝弪蝻⑼躞篚痧禊狒戾狍镱镦洪铊弪轸邃哄翦蝾犰矧洪铘弪钺膦┅啜戾è轸弪狒矧疳汶徵弩轸弪狒矧疳汶徵瀛扉篝К泔钿轸轱铙舂┅磲泸镬弭è轸弪狒矧ī扉篝ф躅汜祆К轸弪狒矧┅棱镤┅ㄥ鲠飙麒孱ê泔眇殪瀛麸痨弼屐红镝洵麸痨弼屐哄邈豸濠ㄤ彐躅屮疳钿滹簌礅镬鲠疳汶徵蝈篚祠骘蝽怙澌镳糸镱螬戾舄èㄧ孱簌愆ㄦ秕钿ㄧ孱簌愆溴沆狎狒轱瞟眭祠轲戾鲠祯瀛箦赳ㄤ邈灬蜥糸镱怙澌ㄦ轭洵溴沆狎狒轱铙怙澌铋飑啜滹è疳汶徵弩轸弪狒矧疳汶徵К镳糸镱舂骘躅鲠颟铋飑冷邈灬蜥糸镱眭祠轲戾鲠祯瀛箦赳ì骘躅鲠颟ㄦ躅汜祆椹躅戾篌骘躅蝈趱蝾蝈篚祠骘蝽┅棱镤┅┅ㄤ彐磲泸滹簌礅镬è鲠镳糸镱犰疳汶徵И疳汶徵濯蝈篚祠骘蝽铋飑蝈篝怙澌⒂铘狲ㄤ锃簌礅镬鲠垧徙脶珏垓弩蹯糨荸溴沆翎篝狒屙孱酏砒邈豸弩釉猎磐盼泽镱沐骘遽汨簌礅镬轭辛盟燎麒殂溴驷蹯趔麸翳沲蝌孱疳汶徵濠鏖翳至怙躅麸翳簌礅镬澡孱弼犰踽翦遗诱淘麒殂溴驷蹯趔麸紊泰犷蝈趱蝾犰鲠祯弩ㄥ疳钿滹簌礅镬鲠疳汶徵蝈篚祠骘蝽怙澌Ж洪铊弪轸邃洪铘弪钺哄翦蝾犰┅ㄤ彐磲泸滹屮翦蝾犰簌礅镬è鲠镳糸镱犰疳汶徵И疳汶徵濯蝈篚祠骘蝽铋飑蝈篝怙澌⒂铘狲ㄤ锃屮翦蝾犰簌礅镬鲠垧徙脶珏垓弩蹯糨荸溴沆翎篝狒屙孱酏朋翎忪轶桢紊忪镢犷屮邈豸弩釉猎磐盼泽镱沐骘遽汨屮翦蝾犰簌礅镬轭辛盟燎麒殂溴驷蹯趔麸翳沲蝌孱疳汶徵濠鏖翳至怙躅麸翳鲠蜷徕戾澡孱弼犰踽翦遗诱淘麒殂溴驷蹯趔麸紊泰犷蝈趱蝾犰鲠祯弩ㄥ疳钿滹簌礅镬鲠疳汶徵蝈篚祠骘蝽怙澌Ж哄翦蝾犰┅ㄤ彐磲泸滹犰飙簌礅镬è鲠镳糸镱犰蝈篚祠骘蝽铋飑蝈篝怙澌⒂铘狲ㄤ锃犰飙簌礅镬鲠垓弩蹯糨溴沆翎篝狒屙孱酏朋翎忪轶桢紊忪镢犷屮邈豸弩釉猎磐盼泽镱沐骘遽汨簌礅镬轭遽汨疳汶徵瀣鏖翳至怙躅麸翳簌礅镬澡孱弼犰踽翦遗诱淘麒殂溴驷蹯趔麸紊泰犷蝈趱蝾犰鲠祯弩ㄥ疳钿滹簌礅镬鲠Ж扉篝犰飙疳汶徵弩蝈篚祠骘蝽怙澌Ж洪铘弪钺哄翦蝾犰┅ㄤ彐躅痱轭舡簌礅镬狃蝻痫簌礅镬痱轭簌礅镬麒孱ㄦ怙躅漯簌礅镬ㄩ箴邈獒飙镳弪狒矧簌礅镬痱轭羽邈獒骘蝽ㄩ磲泸锃骢钽糸镱簌礅镬痱轭歪泸铫痱轭契钽糸镱┅┅ㄣ镱è怙躅漯簌礅镬ㄩㄣ镱篝犷麴簌礅镬痱轭蔑铙翎铘痱轭栳鲠祯搴┅痱轭簌礅镬鲠祯簌礅镬┅è屮艉箴邈獒祓簌礅镬痱轭庆镡犰鲠蜷徕戾躅怙躅洎┅麒孱ㄦ轭洵沆狍簌礅镬铋飑痱轭渺狍螈┅麒孱ㄥ艉豉疱屮疳钿弪簌礅镬痱轭赠疱┅翦蝠蜷┅ㄤ彐躅狃蝻痫篝蜷铉镳糸镱犰疳汶徵濠⒘蜱蠛篝蜷铉镳糸镱犰疳汶徵铋飑序轭趔翳矬簌礅镬麒矬痱轭舡钺礤泔铘衢釉疑吻狍篚怏趄轭绠涉辛盟燎轶铒瞽紊态翳孱镱禊翳箴邈殒殄辛盟燎轶箦狎汨邃箦赳篝蜷铉篝蜷铉篝蜷铉┅磲疸＇痱轭舡簌礅镬狃蝻痫ㄡ痱镳矬扉篝篝蜷铉疳汶徵濠鲠祯弩┅换狃蝻痫蟓扉篝骢钽糸镱骝镯篝狍筢趔术铄哺舶钡ㄤ彐躅狃蝻痫蟓扉篝篝蜷铉镳糸镱犰疳汶徵濠⒘蜱蠛篝蜷铉镳糸镱犰疳汶徵铋飑义趱蝾扉篝镦犰簌礅镬麒矬痱轭舡钺礤泔铘衢釉疑吻狍篚怏趄轭绠涉辛盟燎轶铒瞽紊态翳孱镱禊翳箴邈殒殄辛盟燎轶箦狎汨邃换义盹鲥漉痨殂狒弩箝钽轸箫螋邃翳轶轶驷篝弪翳犷溴戾翦漉痨殂狒弩祜镳鏖翳痱弼轱躞骘轭箫螋ㄡ痱镳矬扉篝轭铄篝蜷铉疳汶徵濠＇篝蜷铉戾篌皓殒铒ㄥ痱弼轱躞┅泔祆邈箦翩痱弼轱躞┅ㄤ彐躅狃蝻痫蟓扉篝轭铄篝蜷铉疳汶徵镳糸镱犰箦孱疳汶徵弩麒孱礤礅弪疳汶徵箦孱疳汶徵弩蝈趱蝾骝镯狃蝻痫蟓扉篝轭铄铋飑箦翩箦孱疳汶徵弩ㄣ镱疳汶徵箦孱疳汶徵弩┅戾舄è扉篝Ж┅篝蜷铉篝蜷铉篝蜷铉┅ㄣ镱疳汶徵ㄤ镬轶疳汶徵瀛躞瀛扉篝疳汶徵濠箦翩扉篝钽镱ㄡ痱镳矬扉篝轭铄篝蜷铉箦孱疳汶徵弩扉篝┅ㄤ锃簌礅镬簌礅镬疳汶徵濠麒孱箦狎汨篝蜷铉篝蜷铉簌礅镬呼弩＇汨狎羼踽飑箦赳扉篝ㄣ镱簌礅镬扉篝┅┅ㄤ锃犰飙簌礅镬簌礅镬麒孱箦狎汨篝蜷铉篝蜷铉簌礅镬呼弩＇汨狎羼踽飑箦赳扉篝ㄣ镱簌礅镬扉篝┅┅┅扉篝┅ㄩ瞽疳汶徵：屮舂ㄤ彐磲泸鏖翳疳汶徵瀛蝈徜祜汶è疳汶徵濠怙澌怙澌啜泔蝈恒犰飙鏖翳疳汶徵瀛蝈徜祜汶疳汶徵灬礅溽ī棱镤┅ㄤ彐磲泸鏖翳疳汶徵瀛蝈徜黩轸瀛祜汶è疳汶徵濠怙澌怙澌啜泔蝈恒犰飙鏖翳疳汶徵瀛蝈徜黩轸瀛祜汶疳汶徵灬礅溽ī棱镤┅ㄤ彐磲泸鏖翳躅祜汶邃疳汶徵弩è蝈篝疳汶徵弩怙澌怙澌换粕赝趴翦汨铋汜祆痱镱麸韵迷险蜥沐蟋换箝钽疳汶徵祜汶滹瞌躞翳疳汶徵祜汶ㄣ镱骢箝铉弩戾è躅祜汶邃疳汶徵弩ㄧ孱簌⒄翁厦伺沫辛盟燎庞┅啜戾è躅祜汶邃疳汶徵弩ī┅躅鏖钿痱雉邈痱镧ㄤ镬轶К疳汶徵弩戾èㄦ轭洵疳汶徵皓┅麒孱疳汶徵瀛祜汶邃皓瘐箬躅祜汶邃疳汶徵弩躅祜汶疳汶徵皓┅棱镤ㄤ镬轶躅祜汶邃疳汶徵弩麒孱疳汶徵瀛钺礤皓磲脲篚蝈轸栳箢忮孱溴戾翦祜汶疳汶徵皓┅┅┅ㄤ彐躅疳汶徵瀛祜汜飙铋汶钺礤疳汶徵瀛溴箝珙狒矧⒁弭躜犷犰轶篝蜷铉疳汶徵濠镦祜汜铋汶钺礤轭翳玳鲥疳汶徵瀹渝犰箫禾厦撂紊盟瘟团镳糸镱麸呐菩撩肆桥戾è疳汶徵ㄦ轭洵疳汶徵疳汶徵瀛溴箝珙狒矧┅麒孱铛祆疳汶徵濠ㄥ蝌矧ю徙脶珏弪蝻吼徙脶珏疳汶徵濠鏖翳疳汶徵瀛蝈徜祜汶疳汶徵濠ㄣ矧搴疳汶徵瀛祜汜飙铋汶钺礤蟓轭翦蝾犰疳汶徵濠┅ㄤ彐躅徜洵疳汶徵瀛祜汜飙铋汶钺礤铋汶钺礤溴箝珙狒矧徙趱犰疳汶徵镳糸镱犰疳汶徵瀛溴箝珙狒矧疳汶徵濯┅⒘滗铋汶钺礤骘徙趱犰疳汶徵瀣祜汜麸翳溴箝珙狒邃疳汶徵瀹娱珙犰泔铘轭踽忪弪蝻殒翳铄铋汶钺礤轶犰蝈徜铋汶钺礤轭翳溴箝珙狒邃疳汶徵骘溟骀弪孱疳汶徵瀹渝犰箫禾厦撂紊盟瘟团镳糸镱麸呐菩撩肆桥戾舄è铋汶钺礤篝蜷铉铋汶钺礤溴箝珙狒矧┅ㄡ泗踽ㄦ轭洵疳汶徵徙趱犰疳汶徵濠疳汶徵ㄦ轭洵疳汶徵疳汶徵瀛溴箝珙狒矧┅镬ㄦ矧沐铋飑麒孱铛祆徙趱犰ㄥ蝌矧ю徙脶珏弪蝻吼徙脶珏徙趱犰疳汶徵濠麒孱铛祆疳汶徵濠ㄥ蝌矧ю徙脶珏弪蝻吼徙脶珏疳汶徵瀛溴箝珙狒矧┅翎玮镤祜镳鏖翳疳汶徵瀛蝈徜黩轸瀛祜汶疳汶徵濠戾舄è祜汜祗ㄣ矧搴疳汶徵瀛祜汜飙铋汶钺礤蟓轭翦蝾犰疳汶徵濠ㄥ轶糸铉ㄡ篌镢铋汶钺礤祜汜祗呼弩＇篝蜷铉僵┅ㄣ镱è铛祆屮轶糸铉瘐箬ㄣ镱铋汶钺礤徙趱犰ㄣ矧搴疳汶徵瀛祜汜飙铋汶钺礤蟓轭翦蝾犰疳汶徵濠┅è羼ㄣ潋屮轶糸铉徙趱犰┅犰蝈徜轭翳弪寤滹铒翳轭绠ㄦ矧沐箦翩ㄣ潋屮轶糸铉徙趱犰┅箦翩镬ㄣ潋屮轶糸铉┅ㄧ弪颟┅┅ㄧ滹铄弪箝珙犰弪蝻蝮鏖翳秕栾熹轭翳祜汶蝈篝狎舡汜箦ㄥ蝌矧с矧搴箝眇戾疳汶徵瀛弪蝻吼徙脶珏疳汶徵烘矧磲舡泔铘蝻⒚犷铒徜狍祜汜铋汶钺礤骘轭岷轸犰蝈徜祜汜铋汶钺礤骘岙烘矧磲舡狎珲礤铘扉篝铋汶钺礤疳汶徵瀛钺礤徙趱犰疳汶徵瀛钺礤疳汶徵濠疳汶徵瀛钺礤镬洎┅脲屦镬ī候屦矧灬礅溽螬ㄦ矧磲⑺邋狍祜汜铋汶钺礤骘岙铋汶钺礤疳汶徵瀛钺礤镬洎┅ㄣ栳铉瀛铋汶ī候屦矧灬礅溽螬ㄦ矧磲⒄箦狍祜汜铋汶钺礤骘轭篝遽洚铋汶钺礤疳汶徵瀛钺礤徙趱犰┅箦翩骘蜚舂ㄧ祜镳┅滹铄疳汶徵濠ㄤ彐躅蝈盹鲥疳汶徵瀛祜汜飙铋汶钺礤铋汶钺礤溴箝珙狒矧镳糸镱犰疳汶徵瀛溴箝珙狒矧疳汶徵濯┅⑸翳溴箝珙狒邃疳汶徵栳翳铋汶钺礤祜汜祆轸轶蝈盹鲥犷趄蹂鲠祯轶蝈趱蝾邃萧桢蝼轶紊轶蝈趱蝾邃渝犰箫禾厦撂紊盟瘟团镳糸镱麸呐菩撩肆桥戾è疳汶徵ㄦ轭洵疳汶徵疳汶徵瀛溴箝珙狒矧┅铋汶钺礤篝蜷铉铋汶钺礤溴箝珙狒矧┅麒孱铛祆疳汶徵濠ㄥ蝌矧ю徙脶珏弪蝻吼徙脶珏疳汶徵濠鏖翳疳汶徵瀛蝈徜黩轸瀛祜汶疳汶徵濠戾舄è祜汜祗ㄣ矧搴疳汶徵瀛祜汜飙铋汶钺礤蟓轭翦蝾犰疳汶徵濠疳轵ㄡ篌镢铋汶钺礤祜汜祗呼弩＇篝蜷铉僵┅麒孱疳轵屐箦蝈趱蝾紊箦翩ㄣ矧搴疳汶徵瀛祜汜飙铋汶钺礤蟓轭翦蝾犰疳汶徵濠蝈盹鲥疳轵祜汜祗呼弩＇羼┅舂┅┅ㄤ彐躅疳汶徵瀛祜汜祆铋汶钺礤洵怡扉篝疳汶徵瀛溴箝珙狒矧⒁弭躜扉篝镦疳汶徵弩翳狒栳鲥祜汜铋汶钺礤骘翳溴箝珙狒邃疳汶徵瀹渝犰箫禾厦撂紊盟瘟团镳糸镱麸呐菩撩肆桥戾è疳汶徵ㄦ轭洵疳汶徵疳汶徵瀛溴箝珙狒矧┅蝈篚祠铋飑麒孱铛祆疳汶徵濠ㄥ蝌矧ю徙脶珏弪蝻吼徙脶珏疳汶徵濠ㄤ镬轶扉篝犰飙疳汶徵弩蝈篚祠麒孱ㄦ轭疳汶徵疳汶徵瀛祜汜飙铋汶钺礤皓弘妁＇沅呼弩＇羼瘐箬蝈篚祠┅┅ㄤ彐躅疳汶徵瀛轫痨屙孱趔扉篝疳汶徵瀛溴箝珙狒矧⒁弭躜扉篝镦疳汶徵弩翳狒翳溴箝珙狒邃疳汶徵轫痨屙孱趔渝犰箫荷托膛团卧镳糸镱麸呐菩撩肆桥戾è疳汶徵ㄦ轭洵疳汶徵疳汶徵瀛溴箝珙狒矧┅蝈篚祠铋飑麒孱铛祆疳汶徵濠ㄥ蝌矧ю徙脶珏弪蝻吼徙脶珏疳汶徵濠ㄤ镬轶扉篝犰飙疳汶徵弩蝈篚祠麒孱ㄦ轭疳汶徵疳汶徵瀛轫痨屙孱翦洵怡扉篝皓瘐箬蝈篚祠┅┅